use std::rc::Rc;

use num_bigint::BigUint;

use syntax::cst::*;

use super::util::span;

#[LALR]
grammar;

// Tokens
match {
    // Symbols
    "(", ")", "{", "}", ";", ":=", "=>", ",", ":", ".", "?",
    // Names
    "_",
    r"[a-zα-ω][a-zα-ωA-ZΑ-Ω0-9_]*['₀₁₂₃₄₅₆₇₈₉]*",
    r"[A-ZΑ-Ω][a-zα-ωA-ZΑ-Ω0-9_]*['₀₁₂₃₄₅₆₇₈₉]*",
    // Literals
    r"0|[1-9][0-9]*",
    // Keywords
    "data", "codata",
    "def", "codef",
    "match", "as", "comatch",
    "absurd",
    "Type",
    // Comments and whitespace
    r"\s*" => { }, // Skip whitespace
    r"--[^\n\r]*[\n\r]*" => { }, // Skip `--` comments
}

// Utils
// Based in part on Gluons grammar (https://github.com/gluon-lang/gluon/blob/fac08dcff7a019440dd8d0a886abdb18baa58198/parser/src/grammar.lalrpop)

Comma<Rule>: Vec<Rule> =
    <rules: (<Rule> ",")*> <last: Rule?> => {
        let mut rules = rules;
        rules.extend(last);
        rules
    };

Parens<Rule>: Rule =
    "(" <rule: Rule> ")" => rule;

Braces<Rule>: Rule =
    "{" <rule: Rule> "}" => rule;

ParenthesizedArgs<Rule>: Vec<Rule> = Parens<Comma<Rule>>;
OptParenthesizedArgs<Rule>: Vec<Rule> = <args: Parens<Comma<Rule>>?> => args.unwrap_or_default();

BracedArgs<Rule>: Vec<Rule> = Braces<Comma<Rule>>;
OptBracedArgs<Rule>: Vec<Rule> = <args: Braces<Comma<Rule>>?> => args.unwrap_or_default();

Param: Param = <name: Ident> <names: Ident*> ":" <typ: Exp> => Param { name, names, typ } ;

ParamInst: ParamInst = <l: @L> <name: Ident> <r: @R> => ParamInst { info: span(l, r), name };

Params: Vec<Param> = ParenthesizedArgs<Param>;
OptParams: Vec<Param> = OptParenthesizedArgs<Param>;

Telescope: Telescope = <params: Params> => Telescope(params);
OptTelescope: Telescope = <params: OptParams> => Telescope(params);

TelescopeInst: TelescopeInst = <params: Parens<Comma<ParamInst>>> => TelescopeInst(params);
OptTelescopeInst: TelescopeInst = <params: Parens<Comma<ParamInst>>?> => TelescopeInst(params.unwrap_or_default());

Args: Vec<Rc<Exp>> = ParenthesizedArgs<Exp>;
OptArgs: Vec<Rc<Exp>> = OptParenthesizedArgs<Exp>;

// Program

pub Prg: Prg = {
    <items: Item*> <exp: Exp?> => Prg { items, exp },
}

pub Item: Item = {
    <decl: TypDecl> => Item::Type(decl),
    <decl: DefDecl> => Item::Def(decl),
}

pub TypDecl: TypDecl = {
    <l: @L> "data" <name: UpperIdent> <params: OptTelescope> "{" <ctors: Comma<Ctor>> "}" <r: @R> => TypDecl::Data(Data { info: span(l, r), name, params, ctors }),
    <l: @L> "codata" <name: UpperIdent> <params: OptTelescope> "{" <dtors: Comma<Dtor>> "}" <r: @R> => TypDecl::Codata(Codata { info: span(l, r), name, params, dtors }),
}

pub DefDecl: DefDecl = {
    <l: @L> "def" <scrutinee: Scrutinee> "." <name: LowerIdent> <params: OptTelescope> ":" <ret_typ: Exp> "{" <body: Match> "}" <r: @R> => DefDecl::Def(Def { info: span(l, r), name, params, scrutinee, ret_typ, body }),
    <l: @L> "codef" <name: UpperIdent> <params: OptTelescope> ":" <typ: TypApp> "{" <body: Comatch> "}" <r: @R> => DefDecl::Codef(Codef { info: span(l, r), name, params, typ, body }),
}

Ctor: Ctor = {
    <l: @L> <name: UpperIdent> <params: OptTelescope> <typ: (":" <TypApp>)?> <r: @R> => Ctor { info: span(l, r), name, params, typ },
}

Dtor: Dtor = {
    <l: @L> <destructee: Destructee> <name: LowerIdent> <params: OptTelescope> ":" <ret_typ: Exp> <r: @R> => Dtor { info: span(l, r), name, params, destructee, ret_typ },
}

pub Comatch : Comatch = {
    <l: @L> <cases: Comma<Cocase>> <r: @R> => Comatch { info: span(l, r), cases },
}

pub Match : Match = {
    <l: @L> <cases: Comma<Case>> <r: @R> => Match { info: span(l, r), cases },
}

pub Case : Case = {
    <l: @L> <name: Ident> <args: OptTelescopeInst> <body: AbsurdOrBody> <r: @R> => Case { info: span(l, r), name, args, body },
}

pub Cocase : Cocase = {
    <l: @L> <name: Ident> <args: OptTelescopeInst> <body: AbsurdOrBody> <r: @R> => Cocase { info: span(l, r), name, args, body },
}

pub AbsurdOrBody: Option<Rc<Exp>> = {
    "absurd" => None,
    "=>" <body: Exp> => Some(body),
}

pub Destructee: Destructee = {
    <l: @L> <scrutinee: (<Scrutinee> ".")?> <r: @R> => match scrutinee {
        Some(Scrutinee { info, name, typ }) => Destructee { info, name, typ: Some(typ) },
        None => Destructee { info: span(l, r), name: None, typ: None },
    }
}

pub Scrutinee: Scrutinee = {
    <l: @L> "(" <name: Ident> ":" <typ: TypApp> ")" <r: @R> => Scrutinee { info: span(l, r), name: Some(name), typ },
    <l: @L> <typ: TypApp> <r: @R> => Scrutinee { info: span(l, r), name: None, typ },
}

pub TypApp: TypApp = {
    <l: @L> <name: UpperIdent> <args: OptArgs> <r: @R> => TypApp { info: span(l, r), name, args },
}

pub Exp: Rc<Exp> = {
    <l: @L> <exp: Ops> ":" <typ: Exp> <r: @R> => Rc::new(Exp::Anno { info: span(l, r), exp, typ }),
    Ops,
}

pub Ops = {
    <l: @L> <exp: Ops> "." <name: Ident> <args: OptArgs> <r: @R> => Rc::new(Exp::DotCall { info: span(l, r), exp, name, args }),
    <l: @L> <on_exp: Ops> "." "match" <name: Ident?> <motive: Motive?> "{" <body: Match> "}" <r: @R> => Rc::new(Exp::Match { info: span(l, r), name, on_exp, motive, body }),
    App,
}

pub App = {
    <l: @L> <name: Ident> <args: Args> <r: @R> => Rc::new(Exp::Call { info: span(l, r), name, args }),
    <l: @L> "comatch" <name: Ident?> "{" <body: Comatch> "}" <r: @R> => Rc::new(Exp::Comatch { info: span(l, r), name, body }),
    Builtins,
}

pub Builtins: Rc<Exp> = {
    <l: @L>"Type" <r: @R> => Rc::new(Exp::Type { info: span(l, r) }),
    Hole,
}

pub Hole: Rc<Exp> = {
    <l: @L>"?" <r: @R> => Rc::new(Exp::Hole { info: span(l,r) }),
    Atom,
}

pub Atom: Rc<Exp> = {
    <l: @L> <s: r"0|[1-9][0-9]*"> <r: @R> => Rc::new(Exp::NatLit { info: span(l, r), val: BigUint::parse_bytes(s.as_ref(), 10).unwrap() }),
    "(" <exp: Exp> ")" => exp,
    <l: @L> <name: Ident> <r: @R> => Rc::new(Exp::Call { info: span(l, r), name, args: Args::new() }),
}

Motive: Motive = {
    <l: @L> "as" <name: Ident> "=>" <ret_typ: Exp> <r: @R> => Motive { info: span(l, r), name, ret_typ },
}

// Names
Ident: String = {
    "_" => "_".to_owned(),
    <s: LowerIdent> => s.to_owned(),
    <s: UpperIdent> => s.to_owned(),
}
LowerIdent: String = <s:r"[a-zα-ω][a-zα-ωA-ZΑ-Ω0-9_]*['₀₁₂₃₄₅₆₇₈₉]*"> => s.to_owned();
UpperIdent: String = <s:r"[A-ZΑ-Ω][a-zα-ωA-ZΑ-Ω0-9_]*['₀₁₂₃₄₅₆₇₈₉]*"> => s.to_owned();
